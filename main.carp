(system-include "Arduino.h")

(load "git@github.com:TimDeve/hoquet@f9cf3ee3cf25025839d17ae2cfec2024263e292d")

(load "config.carp")
(load "secrets.carp")

(load "src/serial.carp")
(load "src/wifi.carp")
(load "src/server.carp")
(load "src/sys.carp")
(load "src/void.carp")

(use Array)
(use Wifi.WifiStatus)

(defn print-scanned-ssids []
  (let [numberOfNetwork (Wifi.scan-network)]
    (do
     (for [networkIndex 0 numberOfNetwork]
       (Serial.println &(Wifi.get-ssid networkIndex)))
     (Serial.println "--------------"))))

(defn generate-html-hello []
  (with Hoquet
    (h "html"
       {}
       [(h "body"
           {}
           [(h "h1" {} [@"Hello from esp8266"])
            (h "p" {} [@"Welcome to embedded Carp."])])])))

(sig setup (Fn [] ()))
(defn setup []
  (do
   (Serial.init 74880l)
   (Wifi.disconnect)
   (Wifi.set-mode (Wifi.WifiMode.Station))
   (print-scanned-ssids)
   (void (Wifi.begin &(Secrets.wifi-ssid) &(Secrets.wifi-pass)))))

(sig loop (Fn [] ()))
(defn loop []
  (do
   (match (Wifi.get-status)
     Connected (do
                  (Serial.println &(String.append
                                     "Connected, ip = "
                                     &(Wifi.get-local-ip)))
                  (Server.serve &(generate-html-hello)))
     _         (do
                  (Serial.println &(String.append
                                     "Connecting to: "
                                     &(Secrets.wifi-ssid)))
                  (Sys.delay 1000)))))

