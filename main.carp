(system-include "Arduino.h")

(load "config.carp")
(load "secrets.carp")

(load "src/serial.carp")
(load "src/wifi.carp")
(load "src/sys.carp")

(use Array)
(use Wifi.WifiStatus)

(defn print-scanned-ssids []
  (let [numberOfNetwork (Wifi.scan-network)]
    (do
     (for [networkIndex 0 numberOfNetwork]
       (Serial.println &(Wifi.get-ssid networkIndex)))
       (Serial.println "--------------"))))


(sig setup (Fn [] ()))
(defn setup []
  (do
   (Serial.init 74880l)
   (Wifi.disconnect)
   (Wifi.set-mode (Wifi.WifiMode.Station))
   (print-scanned-ssids)
   ; Awkard let to discard result of Wifi.begin
   (let [_ (Wifi.begin &(Secrets.wifi-ssid) &(Secrets.wifi-pass))] ())))

(sig loop (Fn [] ()))
(defn loop []
  (do
   (Sys.delay 1000)
   (Serial.println &(match (Wifi.get-status)
                     Connected (String.append
                                 "Connected, ip = "
                                 &(Wifi.get-local-ip))
                     _         (String.append
                                 "Connecting to: "
                                 &(Secrets.wifi-ssid))))))

